print "int7 start";
//set MODE flag to system call number which is 5 for write system call.
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+1]*16+9] = 5;

//store value of user SP for future computations
alias userSP R0;
userSP = SP;

//switch stack from user stack to kernel stack
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1] * 16) + 13] = SP;
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1] * 16) + 11]*512 - 1;

//checking whether arg2(file descriptor) is -2 or not.
//arg2 is in userSP-4. Manual address translation done to get value at UserSP-4.
alias physicalPageNum R1;
alias offset R2;
alias fileDescPhysicalAddr R3;
physicalPageNum = [PTBR + 2 * ((userSP - 4)/ 512)];
offset = (userSP - 4) % 512;
fileDescPhysicalAddr = (physicalPageNum * 512) + offset;
alias fileDescriptor R4;
fileDescriptor=[fileDescPhysicalAddr];

if (fileDescriptor != -2)
then
     	//store -1 to return.
     	alias physicalAddrRetVal R5;
     	physicalAddrRetVal = [PTBR + 2 * (userSP-1)/512]*512 + (userSP-1)%512;
     	[physicalAddrRetVal] = -1;
else
     	//calculate physicalAddress of Arg2 and extract value from it to print it to terminal
     	alias word R5;
     	word = [[PTBR + 2 * ((userSP - 3) / 512)] * 512 + ((userSP - 3) % 512)];

	//------------------------------------------------------------------------------
	// push all registers used till now in kernel stack
	multipush(R0, R1, R2, R3, R4, R5); // number of registers will depend on your code
	
	//store function number of terminal write in R1
	R1 = TERMINAL_WRITE;//TERMINAL_WRITE = 3
	
	//store PID of current process in R2
	R2 = [SYSTEM_STATUS_TABLE+1];
	
	// store word to be printed in R3
	//word = [[PTBR + 2 * ((userSP - 3) / 512)] * 512 + ((userSP - 3) % 512)];
	R3 = word;
	
	print "call mod4";
	//call module 4
	call MOD_4;
	print "endcall mod4";
	
	multipop(R0, R1, R2, R3, R4, R5); 
	//------------------------------------------------------------------------------

     	//set return value 0 indicating success
     	alias physicalAddrRetVal R6;
     	physicalAddrRetVal = ([PTBR + 2 * (userSP - 1)/ 512] * 512) + ((userSP - 1) % 512);
     	[physicalAddrRetVal] = 0;
endif;

//set SP to userSP
SP = userSP;

//set mode value to 0
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;

//pass control back
ireturn;
